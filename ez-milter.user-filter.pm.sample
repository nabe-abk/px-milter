use strict;
################################################################################
# user filter main
################################################################################
# This file is automatically reloaded.
#
package user_filter;
#-------------------------------------------------------------------------------
my %USER = map { $_ => 1 } qw(
	username@example.jp
);
#-------------------------------------------------------------------------------
use constant ACCEPT     =>   0;
use constant CONTINUE   =>   0;		# same as ACCEPT
use constant NO_CHECK   =>   1;		# No check filter
use constant IS_SPAM    =>   2;		# default SPAM action
use constant ADD_HEADER =>  -1;		# force add header and email is accept
use constant REJECT     => 101;		# force REJECT   =SMFIS_REJECT
use constant DISCARD    => 102;		# force DISCARD  =SMFIS_DISCARD
use constant TEMPFAIL   => 104;		# force TEMPFAIL =SMFIS_TEMPFAIL
################################################################################
# main
################################################################################
# Ret:
#	0	accept (not SPAM)
#	1	detect SPAM
#
sub main {
	my $arg      = shift;
	my $c_name   = $arg->{c_name};		# HELO hostname
	my $c_ip     = $arg->{c_ip};
	my $c_host   = $arg->{c_host};		# lookup hostname
	my $env_from = $arg->{env_from};
	my $rcpt_to  = $arg->{rcpt_to};
	my $to_name  = $arg->{to_name};
	my $from_name= $arg->{from_name};
	my $header   = $arg->{header};		# hash ref
	my $body     = $arg->{body};
	my $html     = $arg->{html};
	my $attaches = $arg->{attaches};	# array ref

	if (! $USER{$rcpt_to}) {
		return NO_CHECK;
	}

	# setted "to name"
	if ($to_name ne '' && $to_name ne $rcpt_to && $to_name ne ($rcpt_to =~ s/\@.*//rg)) {
		return ACCEPT;
	}

	# check PowerMTA
	if ($c_host eq 'unknown') {
		my $sock = $arg->try_connect($c_ip, 25);	# IO::Socket::IP object
		if ($sock) {					# https://metacpan.org/pod/IO::Socket
			if ($sock->read_line() =~ /PowerMTA/) {
				return (IS_SPAM, "Decect PowerMTA server");
			}
			$sock->close();
		}
	}

	# fake link
	if ($html) {
		while($html =~ m!<a\s+(?:"[^\"]*"|[^>])*\bhref\s*=\s*"([^\"]+)"[^>]*>\s*(https?://[^<]+?)\s*<!ig) {
			my $url1 = $1;
			my $url2 = $2;
			$url1 =~ s|^https://([^/]*).*$|$1|;	# url -> hostname
			$url2 =~ s|^https://([^/]*).*$|$1|;	# url -> hostname
			if ($url1 ne $url2) {
				return (IS_SPAM, "Decect fake link");
			}
		}
	}

	if ($header->{'x-mailer'} =~ /Foxmail/) {
		return (IS_SPAM, "Decect Foxmail");
	}
	if ($header->{'x-mailer'} =~ /Supmailer/) {
		return (IS_SPAM, "Decect Supmailer");
	}

	return ACCEPT;
}

################################################################################
# pre DATA command
################################################################################
# This function does not need to exist.
# These return value cannot be used: NO_CHECK, IS_SPAM, ADD_HEADER
#
sub check_pre_DATA {
	my $arg      = shift;
	my $c_name   = $arg->{c_name};		# HELO hostname
	my $c_ip     = $arg->{c_ip};
	my $c_host   = $arg->{c_host};		# lookup hostname
	my $env_from = $arg->{env_from};
	my $rcpt_to  = $arg->{rcpt_to};

	if ($arg->{can_check_spf}) {
		#
		# SPF function require Mail::SPF_XS library.
		#	apt-get install libmail-spf-xs-perl
		#
		my $code = $arg->check_spf();			# pass fail softfail none

		if ($code eq 'fail' || $code eq 'softfail') {
			return (REJECT, "SPF $code", '5.7.1', 'SPF fail');
		}
		$arg->add_header('X-SPF-Check', $code);		# add Header
	}
	return CONTINUE;
}

################################################################################
1;
